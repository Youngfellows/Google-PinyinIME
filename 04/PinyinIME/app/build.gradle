apply plugin: 'com.android.application'
android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    aaptOptions {
        noCompress 'dat'
    }
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30
        applicationId "com.android.inputmethod.pinyin"
        versionCode 9001146
        versionName "9.0011.46"

        externalNativeBuild {
            cmake {
                cppFlags "-Wall"
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
    }


    signingConfigs {
        release {
            storeFile file('..\\keystore\\aosp-releasekey.jks')
            storePassword 'android'
            keyPassword 'android'
            keyAlias = 'AOSP Release'
            v1SigningEnabled true
            v2SigningEnabled true
        }
        debug {
            storeFile file('..\\keystore\\aosp-releasekey.jks')
            storePassword 'android'
            keyPassword 'android'
            keyAlias = 'AOSP Release'
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }

        debug {
            signingConfig signingConfigs.debug
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
            version "3.10.2"
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    preBuild {
        doLast {
            def imlFile = file(project.name + ".iml")
            println 'Change ' + project.name + '.iml order'
            try {
                def parsedXml = (new XmlParser()).parse(imlFile)
                def jdkNode = parsedXml.component[1].orderEntry.find { it.'@type' == 'jdk' }
                parsedXml.component[1].remove(jdkNode)
                def sdkString = "Android API " + android.compileSdkVersion.substring("android-".length()) + " Platform"
                new Node(parsedXml.component[1], 'orderEntry', ['type': 'jdk', 'jdkName': sdkString, 'jdkType': 'Android SDK'])
                groovy.xml.XmlUtil.serialize(parsedXml, new FileOutputStream(imlFile))
            } catch (FileNotFoundException e) {
                // nop, iml not found
            }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                if (buildType.name == 'release' || buildType.name == 'debug') {
                    //def createTime = new Date().format("YYYY-MM-dd", TimeZone.getTimeZone("GMT+08:00"))
                    def createTime = new Date().format("yyyyMMddHHmmss", TimeZone.getTimeZone("GMT+08:00"))
                    //apk名字-版本号-版本名称-生成时间.apk
                    //outputFileName = "PinyinIME" + defaultConfig.versionName + "_" + buildType.name + "_" + createTime + ".apk"
                    outputFileName = "PinyinIME.apk"
                }
            }
        }
    }
}

dependencies {
    //provided files('libs/framework.jar')
    compileOnly  files('libs/framework.jar')
    //
}